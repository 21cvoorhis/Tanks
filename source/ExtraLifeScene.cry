import Graphics2D;
import Graphics2DText;
import Math;
import TwoCansAlpha4;

class ExtraLifeScene : AbstractScene{
	
	field frame = 0;
	
	field levelNames;
	field index;
	field lives;
	field deadTankTiles;
	field deadWallTiles;
	
	field livesText;
	
	field rgbShift = [63, 0, 64];
	
	field timeLeft;
	field pause = false;
	
	constructor(levelNames, index, lives, deadTankTiles, deadWallTiles) : base(){
		
		this.levelNames = levelNames;
		this.index = index;
		this.lives = ++lives;
		this.deadTankTiles = deadTankTiles;
		this.deadWallTiles = deadWallTiles;
		
		this.livesText = FontResource.fromSystem("Times New Roman").getRenderer().setSize(36).setColor(0, 0, 0).render("Extra Life!");
		
		levelString = levelNames[index];
		newLevelString = "";
		
		this.timeLeft = 3.0;
		
	}
	
	function update(inputManager, events){
		
		if(inputManager.isKeyPressedThisFrame(KeyboardKey.P) || inputManager.isKeyPressedThisFrame(KeyboardKey.ESCAPE)){
			this.pause = !this.pause;
		}
		if(this.pause){
			return;
		}
		
		if(this.timeLeft > 0){
			this.timeLeft -= DT;
			this.frame++;
			if(this.frame%2 == 0){
				this.rgbShift = rainbowRGBShift(this.rgbShift.clone());
			}
		}else{
			this.switchSceneFade(new IntroScene(this.levelNames, this.rgbShift, this.index, this.lives, this.deadTankTiles, this.deadWallTiles));
		}
		
	}
	
	function drawExtraLife(){
		
		//starting height and width for the title text
		w = this.livesText.width;
		h = this.livesText.height;
		
		//top left coordinates of the text
		x = (WIDTH/2) - (w/2);
		y = (HEIGHT/2) - (h/2);
		
		borderDraw(x, y, w, h, 24, 16, this.rgbShift.clone(), 255);
		
		this.livesText.draw(x, y);
		
	}
	
	function render(){
		
		Graphics2D.Draw.fill(BR+this.rgbShift[0], BG+this.rgbShift[1], BB+this.rgbShift[2]);
		this.drawExtraLife();
		
		if(this.pause){
			pauseDraw();
		}
		
	}
	
}