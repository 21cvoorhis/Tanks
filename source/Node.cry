import Graphics2D;
import Math;
import Random;
import TwoCansAlpha4;

class Node{
	
	field tile;
	field pos;
	field weight;
	
	field neighbors = []; //I can't spell neighbors consistently, why must it be spelled like this
	
	constructor(t, w){ //t = tile placement
		
		this.tile = t;
		this.pos = tileToPos(t);
		this.weight = w;
		
	}
	
	function getNeighbors(nodes, wallSegments, holeSegments){
		
		this.neighbors = [];
		
		segments = wallSegments.clone();
		segments.concat(holeSegments);
		
		dirs = [[1,0], [0,1], [-1,0], [0,-1], [1,1], [-1,1], [-1,-1], [1,-1]];
		result = [];
		
		for(d : dirs){
			neighbor = [this.tile[0] + d[0], this.tile[1] + d[1]];
			if(tileToIndex(neighbor) != null && findNode(neighbor, nodes) != null && this.clearConnection(segments, this.pos, nodes[tileToIndex(neighbor)].pos)){
				this.neighbors.add(nodes[tileToIndex(neighbor)]);
			}
		}
		
	}
	
	function clearConnection(segments, sourcePos, targetPos){ //exclude is one passed segment to ignore
		
		for(s : segments){
			if(segmentSegmentCollision(s, [sourcePos, targetPos])){
				return false;
			}
		}
		
		return true;
	}
	
	function heuristic(startNode, endNode){ //if they're adjacent, 1, if they aren't, sqrt(2) rounded a bit
		
		x1 = startNode.tile[0];
		y1 = startNode.tile[1];
		x2 = endNode.tile[0];
		y2 = endNode.tile[1];
		
		return pointDistance(x1, y1, x2, y2);
		//Manhattan Distance: return Math.abs(endNode.tile[0] - this.tile[0]) + Math.abs(endNode.tile[1] - this.tile[1]);
	}
	
}